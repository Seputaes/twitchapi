plugins {
    id 'java'
    id 'checkstyle'
    id "com.github.spotbugs" version "1.7.1"
    id 'jacoco'
}

group 'gg.sep'
version '1.0-SNAPSHOT'

sourceCompatibility = 11.0

// variable definitions
ext {
    lombokVersion = "1.18.+"
    checkstyleVersion = '8.19'
    spotbugsVersion = '3.1.12'
}

checkstyle {
    def buildConfigDir = project(':build_config').projectDir
    configFile file("$buildConfigDir/checkstyle/checkstyle.xml")
    configDir file("$buildConfigDir/checkstyle")
    toolVersion project.ext.checkstyleVersion
}

spotbugs {
    toolVersion = project.ext.spotbugsVersion
    ignoreFailures = false
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports.xml.enabled = false
    reports.html.enabled = true
}

jacocoTestReport {
    reports {
        html.enabled true
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.48
            }
        }
    }
}

jacocoTestReport.dependsOn test
check.dependsOn jacocoTestCoverageVerification

test {
    useJUnitPlatform()
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-api:2.11.+'
    compile 'org.apache.logging.log4j:log4j-core:2.11.+'
    compileOnly "org.projectlombok:lombok:$project.ext.lombokVersion"
    compileOnly "com.github.spotbugs:spotbugs-annotations:$project.ext.spotbugsVersion"
    annotationProcessor "org.projectlombok:lombok:$project.ext.lombokVersion"
    
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.google.guava:guava:27.+'
    implementation 'org.apache.commons:commons-collections4:4.+'
    implementation 'org.apache.commons:commons-lang3:3.+'
    implementation 'org.apache.httpcomponents:httpclient:4.5.7'


    testImplementation 'org.powermock:powermock-module-junit4:2.0.+'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.+'

    testCompile 'org.mockito:mockito-junit-jupiter:2.26.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}
